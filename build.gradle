buildscript {
    ext.kotlin_version = '1.0.3'
    ext.kotlin_test_version = "1.3.3"
    ext.hadoop_version = "2.6.1"
    ext.hive_version = "1.1.0"
    ext.mysql_version = "5.1.39"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'

    group = "io.eels"
    version = "0.80.1"

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'eel-sdk'
                    packaging 'jar'
                    description 'Eel Hive HDFS Library'
                    url 'http://www.github.com/eel-sdk/eel'

                    scm {
                        connection 'scm:git:http://www.github.com/eel-sdk/eel'
                        developerConnection 'scm:git:http://github.com/sksamuel'
                        url 'http://www.github.com/eel-sdk/eel'
                    }

                    licenses {
                        license {
                            name 'Apache License Version 2.0'
                            url 'https://opensource.org/licenses/Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'sksamuel'
                            name 'Stephen Samuel'
                            email 'sam@sksamuel.com'
                        }
                    }
                }
            }
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

project(':eel-core') {

    dependencies {
        compile 'com.typesafe:config:1.3.0'
        compile "com.univocity:univocity-parsers:2.0.0"
        compile "com.h2database:h2:1.4.192"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        compile "org.apache.hadoop:hadoop-common:$hadoop_version"
        compile "org.apache.hadoop:hadoop-hdfs:$hadoop_version"
        compile "org.apache.hadoop:hadoop-mapreduce:$hadoop_version"
        compile "org.apache.hadoop:hadoop-mapreduce-client:$hadoop_version"
        compile "org.apache.hadoop:hadoop-mapreduce-client-core:$hadoop_version"
        compile "org.apache.hadoop:hadoop-yarn:$hadoop_version"
        compile("org.apache.hive:hive-exec:$hive_version") {
            exclude group: "org.pentaho"
            exclude group: "org.apache.calcite"
        }
        compile 'org.apache.parquet:parquet-avro:1.8.1'
        compile 'org.slf4j:slf4j-api:1.7.21'
        compile "one.util:streamex:0.6.0"
        compile 'io.reactivex:rxjava:1.1.5'
        compile "io.dropwizard.metrics:metrics-core:3.1.2"
        compile "io.dropwizard.metrics:metrics-jvm:3.1.2"
        testCompile "io.kotlintest:kotlintest:$kotlin_test_version"
        testCompile 'org.slf4j:slf4j-log4j12:1.7.21'
        testCompile 'log4j:log4j:1.2.17'
        testCompile "mysql:mysql-connector-java:$mysql_version"
    }
}